pipeline {
    agent any
    tools {
        gradle 'gradle-8.4'
        jdk 'jdk-21'
    }
    environment {
        IMAGE_NAME = 'user-api'
        IMAGE_TAG = "v${BUILD_NUMBER}"
        BLUE_CONTAINER = "user-api-blue"
        GREEN_CONTAINER = "user-api-green"
        WORKSPACE = "/var/jenkins_home/workspace/laundreader-prod"
        DOCKER_COMPOSE_PATH = "/secure-submodule/docker/docker-compose.yml"
        USER_API_DOCKERFILE_PATH = "/secure-submodule/docker/user-api.Dockerfile"
        NGINX_UPSTREAM_CONF = "/etc/nginx/conf.d/user-api-upstream.conf"
    }
    stages {
        stage('Checkout') {
            steps {
                 // ÏÑúÎ∏åÎ™®Îìà Ìè¨Ìï® checkout
                checkout([
                    $class: 'GitSCM',
                    branches: [[name: '*/develop']],
                    userRemoteConfigs: [[
                        url: 'https://github.com/Laundreader/server.git',
                        credentialsId: 'github-token'
                    ]],
                    extensions: [
                        [$class: 'SubmoduleOption', recursiveSubmodules: true]
                    ]
                ])
            }
        }
        stage('Build JAR') {
            steps {
                sh "chmod +x ./gradlew"
                sh './gradlew :user-api:buildNeeded --stacktrace --info -x test'
            }
        }
        stage('Docker Build'){
            steps {
                sh """
                    docker build \
                        -f ${WORKSPACE}${USER_API_DOCKERFILE_PATH} \
                        -t ${IMAGE_NAME}:${IMAGE_TAG} \
                        -t ${IMAGE_NAME}:latest \
                        ${WORKSPACE}/user-api/build/libs
                """
            }
        }
        stage('Deploy') {
            steps {
                script {
                    //  Î¨¥Ï§ëÎã® ÏÑ§Ï†ï Ï†Ñ GREEN_CONTAINERÎ•º 8080ÏúºÎ°ú ÎùÑÏö∞Í≥† ÏûàÏóàÏùå. Í∑∏Ïóê ÎßûÍ≤å Îß§Ìïë
                    def containerPortMap = [
                        (GREEN_CONTAINER): 8080,
                        (BLUE_CONTAINER) : 8081
                    ]
                    // ÌòÑÏû¨ Ïã§Ìñâ Ï§ëÏù∏ Ïª®ÌÖåÏù¥ÎÑà ÌôïÏù∏ 
                    def active = sh(
                        script: "docker ps --filter 'name=${GREEN_CONTAINER}' --filter 'name=${BLUE_CONTAINER}' --format '{{.Names}}'",
                        returnStdout: true
                    ).trim()

                     // ÌòÑÏû¨Í∞Ä ÏóÜÏúºÎ©¥ BLUE ‚Üí Ï≤´ Î∞∞Ìè¨
                    def next = (!active || active == BLUE_CONTAINER) ? GREEN_CONTAINER : BLUE_CONTAINER

                    // ÏÉà Ïª®ÌÖåÏù¥ÎÑà Ìè¨Ìä∏ Í≤∞Ï†ï
                    def active_port = active ? containerPortMap[active] : null
                    def next_port = containerPortMap[next]

                    echo "‚ñ∂Ô∏è Active container: ${active ?: 'None'} ${active_port ? "(port ${active_port})" : ''}"
                    echo "üîÑ Next container: ${next} (port ${next_port})"

                    // ÏÉà Ïª®ÌÖåÏù¥ÎÑà ÏãúÏûë
                    sh "docker run -d --name ${next} -p ${next_port}:8080 ${IMAGE_NAME}:latest"

                    // Ïª®ÌÖåÏù¥ÎÑà Ï†ïÏÉÅ Íµ¨Îèô Ï≤¥ÌÅ¨
                    sh """
                        for i in {1..10}; do
                            curl -fs http://localhost:${next_port}/health && break
                            echo 'Waiting for container...'
                            sleep 3
                        done || { echo '‚ùå Container failed to start'; exit 1; }
                    """

                    // Nginx upstream Í∞±Ïã† Î∞è reload
                    sh """
                        echo 'upstream user_api_upstream { server localhost:${next_port}; }' > ${NGINX_UPSTREAM_CONF}
                    """

                    sshPublisher(
                        publishers: [
                            sshPublisherDesc(
                                configName: 'host',
                                transfers: [
                                    sshTransfer(
                                        execCommand: 'sudo systemctl reload nginx',
                                        execTimeout: 120000
                                    )
                                ],
                                verbose: true
                            )
                        ]
                    )

                    if(active) {
                        echo "üõë Stopping old container: ${active}"
                        sh "docker rm -f ${active} || true"
                    }

                    echo "‚úÖ Traffic switched to ${next} (port ${next_port}) via Nginx"
                }
            }
        }
    }
    post {
        always {
            echo 'ÌååÏù¥ÌîÑÎùºÏù∏ Ï¢ÖÎ£å'
        }
    }
}
